
@ВПодсистеме
метод ОтправитьПисьмо(Получатели: Строка, Тема: Строка, Тело: Строка)
    
    знч УчетнаяЗапись = СистемнаяУчетнаяЗапись()
    
    знч Сообщение = новый Письмо(УчетнаяЗапись.АдресОтправителя, Получатели, Тема)
    Сообщение.УстановитьТекстКакСтроку(Тело)
    
    знч Аутентификация = новый АутентификацияПочтыПоПаролю(УчетнаяЗапись.Логин, УчетнаяЗапись.Пароль)
    знч ПараметрыПодключения = новый ПараметрыПодключенияSmtp(УчетнаяЗапись.Сервер, УчетнаяЗапись.Порт, Аутентификация)
    
    КлиентSmtp.Отправить(ПараметрыПодключения, Сообщение)
    
;

@Локально
структура УчетнаяЗапись
    пер АдресОтправителя: Строка
    пер Логин: Строка
    пер Пароль: Строка
    пер Сервер: Строка
    пер Порт: Число
;

@Локально
метод СистемнаяУчетнаяЗапись(): УчетнаяЗапись
    
    знч Запрос = Запрос{
        ВЫБРАТЬ ПЕРВЫЕ 1
            Логин,
            АдресОтправителя,
            СерверSMTP,
            ПортSMTP,
            Пароль
        ИЗ
            УчетныеЗаписиЭлектроннойПочты
        ГДЕ
            Использовать
    }
    
    исп РезультатЗапроса = Запрос.Выполнить()
    
    если РезультатЗапроса.Пусто()
        выбросить новый ИсключениеВалидации("Не заполнены настройки электронной почты")
    ;
    
    знч СтрокаРезультата = РезультатЗапроса.Единственный()
    
    знч Результат = новый УчетнаяЗапись()
    Результат.АдресОтправителя = СтрокаРезультата.АдресОтправителя
    Результат.Логин = СтрокаРезультата.Логин
    Результат.Сервер = СтрокаРезультата.СерверSMTP
    Результат.Порт = СтрокаРезультата.ПортSMTP
    Результат.Пароль = СтрокаРезультата.Пароль.Раскрыть()
    
    возврат Результат
    
;
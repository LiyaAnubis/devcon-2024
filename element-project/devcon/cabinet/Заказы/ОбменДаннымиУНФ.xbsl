
метод PingЛюбойМетодОбработкаЗапроса(Запрос: HttpСервисЗапрос)
    Запрос.Ответ.УстановитьТело("Ok")
;

метод ExchangeDataGETОбработкаЗапроса(Запрос: HttpСервисЗапрос)
    
    исп КонтекстДоступа.Привилегированный()
    
    ПроверитьЭтотУзел()
    
    знч КодУзлаУНФ = "smb"
    
    пер Узел = ОбменУНФЛКК.НайтиПоКоду(КодУзлаУНФ)
    
    если Узел == Неопределено
        знч УзелОбъект = новый ОбменУНФЛКК.Объект()
        УзелОбъект.Код = КодУзлаУНФ
        УзелОбъект.Наименование = "Обмен с УНФ"
        УзелОбъект.Записать()
        Узел = УзелОбъект.Ссылка
    ;
    
    знч Запись = новый ЗаписьXml(Запрос.Ответ.ОткрытьПотокЗаписиТела())
    Запись.ЗаписатьНачалоДокумента()
    ОбменУНФЛКК.ВыгрузитьСообщение(Запись, Узел)    
    
;

метод ExchangeDataPOSTОбработкаЗапроса(Запрос: HttpСервисЗапрос)
    
    исп КонтекстДоступа.Привилегированный()
    
    ПроверитьЭтотУзел()
    
    знч Чтение = новый ЧтениеXml(Запрос.Тело)    
    исп ГрупповаяОперация.Начать(РежимЗагрузкиДанных = Истина)
    ОбменУНФЛКК.ЗагрузитьСообщение(Чтение, ДопустимыйНомерСообщенияОбмена.Любой)    
    
;

метод ПроверитьЭтотУзел()
    
    пер КодЭтогоУзла = "lkk"
    
    пер ЭтотУзелОбъект = ОбменУНФЛКК.ПолучитьЭтотУзел().ЗагрузитьОбъект()
    
    если ЭтотУзелОбъект.Код != КодЭтогоУзла
        ЭтотУзелОбъект.Код = КодЭтогоУзла
        ЭтотУзелОбъект.Наименование = "Личный кабинет клиента"
        ЭтотУзелОбъект.Записать()
    ;
    
;
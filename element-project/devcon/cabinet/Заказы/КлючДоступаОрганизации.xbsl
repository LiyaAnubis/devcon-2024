
@Обработчик
метод ПроверитьНаличиеКлючейДоступа(Ключи: ЧитаемыйМассив<КлючДоступаОрганизации.Объект>, Пользователи: ЧитаемыйМассив<Пользователи.Объект>): 
    ЧитаемоеСоответствие<КлючДоступаОрганизации.Объект, ЧитаемаяКоллекция<Пользователи.Объект|Пользователи.Ссылка>>
    
    знч Организации = Ключи.Преобразовать(Ключ -> Ключ.Организация)
    знч ПользователиСсылки = Пользователи.Преобразовать(Пользователь -> Пользователь.Ссылка)
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Представители.Пользователь КАК Пользователь,
            ТЧПредставители.Владелец КАК Организация
        ИЗ
            Организации.Представители КАК ТЧПредставители
                ВНУТРЕННЕЕ СОЕДИНЕНИЕ Представители КАК Представители
                ПО Представители.Ссылка == ТЧПредставители.Представитель
        ГДЕ
            ТЧПредставители.Владелец В (%Организации)
            И Представители.Пользователь В (%ПользователиСсылки)
    }
    
    знч ПредставителиОрганизаций = <Организации.Ссылка, Массив<Пользователи.Ссылка>>{:}
    
    знч РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        знч ПользователиОрганизации = ПредставителиОрганизаций.ВставитьЕслиОтсутствует(СтрокаРезультата.Организация, <Пользователи.Ссылка>[])
        ПользователиОрганизации.Добавить(СтрокаРезультата.Пользователь)
    ;
    
    РезультатЗапроса.Закрыть()
    
    знч Результат = <КлючДоступаОрганизации.Объект, Массив<Пользователи.Ссылка>>{:}
    
    для Ключ из Ключи
        знч МассивПользователей = ПредставителиОрганизаций.ПолучитьИлиУмолчание(Ключ.Организация)
        если МассивПользователей != Неопределено
            Результат.Вставить(Ключ, МассивПользователей)
        ;
    ;    
    
    возврат Результат
    
;
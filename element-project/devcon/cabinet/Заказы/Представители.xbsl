@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [ПользователиСервер.ПолныеПраваНаОбъектДляАутентифицированных()]
;


@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(
    Объекты: ЧитаемыйМассив<Представители.ДанныеРасчетаРазрешений>): Соответствие<Представители.ДанныеРасчетаРазрешений, Массив<РазрешениеДоступа>>
    знч Результат = <Представители.ДанныеРасчетаРазрешений, Массив<РазрешениеДоступа>>{:}

    пер ОрганизацииПредставителей = ОрганизацииПредставителей(Объекты.Преобразовать(Объект -> Объект.Ссылка))

    для Объект из Объекты
        пер Организации = ОрганизацииПредставителей.ПолучитьИлиУмолчание(Объект.Ссылка)

        знч Разрешения = <РазрешениеДоступа>[новый РазрешениеДоступа(
                [новый КлючДоступаПользователя.Объект(Объект.Пользователь)],
                [Сущность.Право.Чтение])]

        если Организации != Неопределено
            Разрешения.Добавить(
                новый РазрешениеДоступа(
                    Организации.Преобразовать(Организация -> новый КлючДоступаОрганизации.Объект(
                                Организация = Организация)),
                    [Сущность.Право.Чтение]))
        ;

        Результат.Вставить(Объект, Разрешения)
    ;

    возврат Результат
;

метод ОрганизацииПредставителей(
    Представители: Массив<Представители.Ссылка>): Соответствие<Представители.Ссылка, Массив<Организации.Ссылка>>
    знч Результат = <Представители.Ссылка, Массив<Организации.Ссылка>>{:}

    пер Запрос = Запрос{
        ВЫБРАТЬ
            Владелец КАК Организация,
            Представитель КАК Представитель
        ИЗ
            Организации.Представители
        ГДЕ
            Представитель В (%Представители)
    }

    пер РезультатЗапроса = Запрос.Выполнить()

    для Строка из РезультатЗапроса
        пер ОрганизацииПредставителя = Результат.ВставитьЕслиОтсутствует(Строка.Представитель, [])
        ОрганизацииПредставителя.Добавить(Строка.Организация)
    ;

    возврат Результат
;

@ВПодсистеме
метод УстановитьВебХук(АдресПриложения: Строка): Строка
    
    знч ДанныеЗапроса = новый Соответствие<Строка, Объект?>()
    ДанныеЗапроса.Вставить("url", "%АдресПриложения/api/tg/send")
    
    знч ТокенВходящегоВызова = новый Ууид().ВСтроку()
    НастройкиПрограммы.УстановитьНастройку(ВидНастройки.ТокенВходящихВызовов, ТокенВходящегоВызова)
    ДанныеЗапроса.Вставить("secret_token", ТокенВходящегоВызова)
    ДанныеЗапроса.Вставить("allowed_updates", 
        ["message", "edited_channel_post", "callback_query", "chat_member"])
    
    знч Тело = СериализацияJson.ЗаписатьОбъект(ДанныеЗапроса)
    
    знч Ответ = ВызватьМетодТелеграм(МетодыТелеграм.УстановитьВебХук(), Тело, ТипСодержимогоJSON())
    
    если Ответ.КодСтатуса != 200
        возврат
            "При установке веб-хука произошла ошибка. Код состояния: %{Ответ.КодСтатуса}
            %{Ответ.Тело.ПрочитатьКакСтроку()}"
    ;
    
    возврат Ответ.Тело.ПрочитатьКакСтроку()
    
;

@ВПодсистеме
метод ПолучитьСостояниеВебХука(): Строка
    
    знч Ответ = ВызватьМетодТелеграм(МетодыТелеграм.ПолучитьСостояниеВебХука())

    если Ответ.КодСтатуса != 200
        возврат
        "При установке веб-хука произошла ошибка. Код состояния: %{Ответ.КодСтатуса}
            %{Ответ.Тело.ПрочитатьКакСтроку()}"
    ;

    возврат Ответ.Тело.ПрочитатьКакСтроку()
   
;

@Глобально
метод УдалитьСообщение(ИдентификаторСообщения: Число, Чат: Чаты.Ссылка)
    
    знч ДанныеЗапроса = новый Соответствие<Строка, Объект?>()
    ДанныеЗапроса.Вставить("chat_id", ИдентификаторЧата(Чат))
    ДанныеЗапроса.Вставить("message_id", ИдентификаторСообщения)
    
    знч Тело = СериализацияJson.ЗаписатьОбъект(ДанныеЗапроса)

    ВызватьМетодТелеграм(МетодыТелеграм.УдалитьСообщение(), Тело, ТипСодержимогоJSON())
    
;

@Глобально
метод УдалитьСообщения(ИдентификаторыСообщений: Массив<Число>, Чат: Чаты.Ссылка)
    
    знч ДанныеЗапроса = новый Соответствие<Строка, Объект?>()
    ДанныеЗапроса.Вставить("chat_id", ИдентификаторЧата(Чат))
    ДанныеЗапроса.Вставить("message_ids", ИдентификаторыСообщений)

    знч Тело = СериализацияJson.ЗаписатьОбъект(ДанныеЗапроса)

    ВызватьМетодТелеграм(МетодыТелеграм.УдалитьСообщения(), Тело, ТипСодержимогоJSON())
    
;


@Глобально
метод ЗаблокироватьПользователя(
    УчетнаяЗапись: УчетныеЗаписиТелеграм.Ссылка, Чат: Чаты.Ссылка, СрокБлокировки:Число? = Неопределено)
    
    знч ДанныеЗапроса = новый Соответствие<Строка, Объект?>()
    ДанныеЗапроса.Вставить("chat_id", ИдентификаторЧата(Чат))
    ДанныеЗапроса.Вставить("user_id", УчетныеЗаписиТелеграм.Идентификатор(УчетнаяЗапись))
    
    если СрокБлокировки != Неопределено
        знч ДатаРазблокировкиUnix = ДатаВремя
            .Сейчас()
            .ДобавитьСекунды(СрокБлокировки)
            .ВМомент(ЧасовойПояс.Текущий())
            .ПолучитьОтметкуВремениUnix()
        ДанныеЗапроса.Вставить("until_date", ДатаРазблокировкиUnix)
    ;       

    знч Тело = СериализацияJson.ЗаписатьОбъект(ДанныеЗапроса)

    ВызватьМетодТелеграм(МетодыТелеграм.ЗаблокироватьУчастникаЧата(), Тело, ТипСодержимогоJSON())
    
;

@Глобально
метод УстановитьРеакциюНаСообщение(РеакцияНаСообщение: РеакцияНаСообщение)
    знч Тело = СериализацияJson.ЗаписатьОбъект(РеакцияНаСообщение.ВОбъектТелеграм())
    ВызватьМетодТелеграм(МетодыТелеграм.УстановитьРеакциюНаСообщение(), Тело, ТипСодержимогоJSON())
;

@ВПодсистеме
метод ИдентификаторЧата(Чат: Чаты.Ссылка): Число
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Чаты.Идентификатор КАК Идентификатор
        ИЗ
            Чаты КАК Чаты
        ГДЕ
            Ссылка == %Чат
    }
    
    возврат Запрос.Выполнить().Единственный().Идентификатор
    
;

@Глобально
метод ОтправитьТекстовоеСообщение(ИсходящееСообщение: ИсходящееСообщение): Число
    
    знч Тело = СериализацияJson.ЗаписатьОбъект(ИсходящееСообщение.ВОбъектТелеграм())
    
    знч Ответ = ВызватьМетодТелеграм(МетодыТелеграм.ОтправитьСообщение(), Тело, ТипСодержимогоJSON())
    
    если Ответ.КодСтатуса != 200
        выбросить новый ИсключениеНедопустимоеСостояние(
            "Ошибка вызова сервера телеграм, код состояния %{Ответ.КодСтатуса}
            Текст ответа: %{Ответ.Тело.ПрочитатьКакСтроку()}"
            )
    ;
    
    знч ДанныеОтвета = СериализацияJson.ПрочитатьОбъект(Ответ.Тело) как Соответствие<Строка, Объект?>
    
    знч Результат = ДанныеОтвета.Получить("result") как Соответствие<Строка, Объект?>
    
    возврат Результат.Получить("message_id") как Число
    
;

@Глобально
метод ОтправитьТекстовоеСообщение(Текст: Строка, Чат: Чаты.Ссылка): Число
    
    знч ИсходящееСообщение = новый ИсходящееСообщение(
        Чат = Чат,
        Текст = Текст)
        
    возврат ОтправитьТекстовоеСообщение(ИсходящееСообщение)
    
;

@Глобально
метод ОтправитьФото(Фото: Байты, ИдентификаторПолучателя: Строка, Подпись = "", 
    Клавиатура: Соответствие<Строка,неизвестно>? = Неопределено): ОтветHttp
    
    пер Токен = Токен()
    
    исп ПотокОтвета = новый ВременныйПотокЗаписи()
    
    пер Разделитель = "-------------573cf973d5228"
    
    ПотокОтвета.Записать("\в\н")
    
    ПотокОтвета.Записать("--%Разделитель\в\н")
    ПотокОтвета.Записать("Content-disposition: form-data; name=\"chat_id\"\в\н")
    ПотокОтвета.Записать("\в\н")
    ПотокОтвета.Записать("%ИдентификаторПолучателя\в\н")
    
    ПотокОтвета.Записать("--%Разделитель\в\н")
    ПотокОтвета.Записать("Content-disposition: form-data; name=\"photo\"; filename=\"image.jpg\"\в\н")
    ПотокОтвета.Записать("Content-Type: image/jpeg\в\н")
    ПотокОтвета.Записать("\в\н")
    ПотокОтвета.Записать(Фото)
    ПотокОтвета.Записать("\в\н")
    
    если не Подпись.Пусто()
        ПотокОтвета.Записать("--%Разделитель\в\н")
        ПотокОтвета.Записать("Content-disposition: form-data; name=\"caption\"\в\н")
        ПотокОтвета.Записать("\в\н")
        ПотокОтвета.Записать("%Подпись\в\н")
    ;
    
    если Клавиатура != Неопределено
        ПотокОтвета.Записать("--%Разделитель\в\н")
        ПотокОтвета.Записать("Content-disposition: form-data; name=\"reply_markup\"\в\н")
        ПотокОтвета.Записать("\в\н")
        ПотокОтвета.Записать("%{СериализацияJson.ЗаписатьОбъект(Клавиатура)}\в\н")
    ;
    
    ПотокОтвета.Записать("--%Разделитель--")
    
    пер Ответ = КлиентHttp.СБазовымUrl("https://api.telegram.org")
        .ЗапросGet("/bot%Токен/sendPhoto")
        .УстановитьТело(ПотокОтвета.ОткрытьПотокЧтения())
        .УстановитьТипСодержимого("multipart/form-data; boundary=%Разделитель")
        .Выполнить()
        
    возврат Ответ
    
;

@ВПодсистеме
метод НачатьЗаписьЛога(ТелоЗапроса: Строка): ИсторияВызововВебХука.Ссылка
    
    знч ЗаписьИстории = новый ИсторияВызововВебХука.Объект()
    знч ДатаВызова = ДатаВремя.Сейчас()

    ЗаписьИстории.Наименование = ДатаВызова.Представление()
    ЗаписьИстории.ТелоЗапроса = 
        СериализацияJson.ЗаписатьОбъект(СериализацияJson.ПрочитатьОбъект(ТелоЗапроса))
    ЗаписьИстории.ДатаВызова = ДатаВызова
    ЗаписьИстории.Записать()
    
    возврат ЗаписьИстории.Ссылка
    
;

@ВПодсистеме
метод ЗавершитьЗаписьЛога(Запись: ИсторияВызововВебХука.Ссылка, КодСтатуса: Число, ТелоОтвета: Строка)
    
    знч ЗаписьИстории = Запись.ЗагрузитьОбъект()

    ЗаписьИстории.КодСостояния = КодСтатуса
    ЗаписьИстории.ТелоОтвета = ТелоОтвета
    ЗаписьИстории.Записать()

;



@ВПодсистеме
метод ВызватьМетодТелеграм(ИмяМетода: Строка, Тело: Строка, ТипСодержимого: Строка): ОтветHttp
    возврат КлиентHttp.СБазовымUrl(АдресСервераТелеграм())
        .ЗапросPost("/bot%{Токен()}/%ИмяМетода")
        .УстановитьТело(Тело)
        .УстановитьТипСодержимого(ТипСодержимого)
        .Выполнить()
;


@ВПодсистеме
метод ВызватьМетодТелеграм(ИмяМетода: Строка, Тело: Файл, ТипСодержимого: Строка): ОтветHttp
    возврат КлиентHttp.СБазовымUrl(АдресСервераТелеграм())
        .ЗапросPost("/bot%{Токен()}/%ИмяМетода")
        .УстановитьТело(Тело)
        .УстановитьТипСодержимого(ТипСодержимого)
        .Выполнить()
;


@ВПодсистеме
метод ВызватьМетодТелеграм(ИмяМетода: Строка, Тело: ПотокЧтения, ТипСодержимого: Строка): ОтветHttp
    возврат КлиентHttp.СБазовымUrl(АдресСервераТелеграм())
        .ЗапросPost("/bot%{Токен()}/%ИмяМетода")
        .УстановитьТело(Тело)
        .УстановитьТипСодержимого(ТипСодержимого)
        .Выполнить()
;


@ВПодсистеме
метод ВызватьМетодТелеграм(ИмяМетода: Строка, Тело: ДвоичныйОбъект, ТипСодержимого: Строка): ОтветHttp
    возврат КлиентHttp.СБазовымUrl(АдресСервераТелеграм())
        .ЗапросPost("/bot%{Токен()}/%ИмяМетода")
        .УстановитьТело(Тело)
        .УстановитьТипСодержимого(ТипСодержимого)
        .Выполнить()
;

@ВПодсистеме
метод ВызватьМетодТелеграм(ИмяМетода: Строка): ОтветHttp
    возврат КлиентHttp.СБазовымUrl(АдресСервераТелеграм())
        .ЗапросGet("/bot%{Токен()}/%ИмяМетода")
        .Выполнить()
;

@Локально
метод Токен(): Строка
    исп КонтекстДоступа.Привилегированный()
    возврат НастройкиПрограммы.ЗначениеНастройки(ВидНастройки.ТокенТелеграммБота)
;

@Локально
метод АдресСервераТелеграм(): Строка
    возврат "https://api.telegram.org"
;

@ВПодсистеме
метод ТипСодержимогоJSON(): Строка
    возврат "application/json"
;

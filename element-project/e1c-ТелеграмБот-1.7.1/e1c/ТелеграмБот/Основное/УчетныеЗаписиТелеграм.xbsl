
@ВПодсистеме
метод ИзОбъектаТелеграм(Отправитель: Соответствие<Строка, неизвестно>): УчетныеЗаписиТелеграм.Ссылка
    пер Запрос = Запрос{
        ВЫБРАТЬ
            УчетныеЗаписиТелеграм.Ссылка КАК Ссылка
        ИЗ
            УчетныеЗаписиТелеграм КАК УчетныеЗаписиТелеграм
        ГДЕ
            УчетныеЗаписиТелеграм.Идентификатор == %{Отправитель.Получить("id")}
    }

    знч НайденныйУчастник = Запрос.Выполнить().ЕдинственныйИлиНеопределено()

    если НайденныйУчастник == Неопределено
        возврат СоздатьУчастника(Отправитель)
    ;

    возврат НайденныйУчастник.Ссылка
;

метод СоздатьУчастника(Отправитель: Соответствие<Строка, неизвестно>): УчетныеЗаписиТелеграм.Ссылка
    знч Участник = новый УчетныеЗаписиТелеграм.Объект()

    Участник.Идентификатор = Отправитель.Получить("id")
    Участник.Логин = Отправитель.ПолучитьИлиУмолчание("username", "")
    Участник.Имя = Отправитель.ПолучитьИлиУмолчание("first_name", "")
    Участник.Фамилия = Отправитель.ПолучитьИлиУмолчание("last_name", "")
    Участник.ЭтоБот = Отправитель.Получить("is_bot")
    Участник.Записать()

    возврат Участник.Ссылка
;

@ВПодсистеме
метод ВОбъектТелеграм(УчетнаяЗаписьТелеграм: УчетныеЗаписиТелеграм.Ссылка): Соответствие<Строка, Объект?>
    
    пер Запрос = Запрос{
        ВЫБРАТЬ
            УчетныеЗаписиТелеграм.Идентификатор КАК Идентификатор
        ИЗ
            УчетныеЗаписиТелеграм КАК УчетныеЗаписиТелеграм
        ГДЕ
            УчетныеЗаписиТелеграм.Ссылка == %УчетнаяЗаписьТелеграм
    }
    
    возврат {"id": Запрос.Выполнить().Единственный().Идентификатор}
        
;

@ВПодсистеме
метод Идентификатор(УчетнаяЗаписьТелеграм: УчетныеЗаписиТелеграм.Ссылка): Число
    
    пер Запрос = Запрос{
        ВЫБРАТЬ
            УчетныеЗаписиТелеграм.Идентификатор КАК Идентификатор
        ИЗ
            УчетныеЗаписиТелеграм КАК УчетныеЗаписиТелеграм
        ГДЕ
            УчетныеЗаписиТелеграм.Ссылка == %УчетнаяЗаписьТелеграм
    }

    возврат Запрос.Выполнить().Единственный().Идентификатор
    
;



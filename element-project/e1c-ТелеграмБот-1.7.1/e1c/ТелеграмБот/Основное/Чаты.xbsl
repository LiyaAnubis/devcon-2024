
@ВПодсистеме
метод ИзОбъектаТелеграм(ЧатТелеграм: Соответствие<Строка, неизвестно>): Чаты.Ссылка
    пер Запрос = Запрос{
        ВЫБРАТЬ
            Чаты.Ссылка КАК Ссылка
        ИЗ
            Чаты КАК Чаты
        ГДЕ
            Чаты.Идентификатор == %{ЧатТелеграм.Получить("id")}
    }

    знч НайденныйЧат = Запрос.Выполнить().ЕдинственныйИлиНеопределено()

    если НайденныйЧат == Неопределено
        возврат СоздатьЧат(ЧатТелеграм)
    ;

    возврат НайденныйЧат.Ссылка
;

метод СоздатьЧат(ЧатТелеграм: Соответствие<Строка, неизвестно>): Чаты.Ссылка
    
    знч ЧастиНаименования: Массив<Строка>
    
    если ЧатТелеграм.СодержитКлюч("title")
        ЧастиНаименования.Добавить(ЧатТелеграм.Получить("title") как Строка)
    ;
    
    если ЧатТелеграм.СодержитКлюч("first_name")
        ЧастиНаименования.Добавить(ЧатТелеграм.Получить("first_name") как Строка)
    ;
    
    если ЧатТелеграм.СодержитКлюч("last_name")
        ЧастиНаименования.Добавить(ЧатТелеграм.Получить("last_name") как Строка)
    ;
    
    если ЧатТелеграм.СодержитКлюч("username")
        ЧастиНаименования.Добавить(ЧатТелеграм.Получить("username") как Строка)
    ;
    
    знч Чат = новый Чаты.Объект()

    Чат.Идентификатор = ЧатТелеграм.Получить("id")
    Чат.Наименование = Строки.Соединить(ЧастиНаименования, " ")
    
    знч ИдентификаторТипаЧата = ЧатТелеграм.Получить("type") как Строка
    если ИдентификаторТипаЧата == "group"
        Чат.ТипЧата = ТипЧата.Группа
    иначе если ИдентификаторТипаЧата == "supergroup"
        Чат.ТипЧата = ТипЧата.Супергруппа
    иначе если ИдентификаторТипаЧата == "channel"
        Чат.ТипЧата = ТипЧата.Канал
    иначе
        Чат.ТипЧата = ТипЧата.Личный
    ;
    
    Чат.Записать()

    возврат Чат.Ссылка    
    
;

@ВПодсистеме
метод ТипЧата(Чат: Чаты.Ссылка): ТипЧата
    
    пер Запрос = Запрос{
        ВЫБРАТЬ
            Чаты.ТипЧата КАК ТипЧата
        ИЗ
            Чаты КАК Чаты
        ГДЕ
            Чаты.Ссылка == %Чат
    }
    
    исп РезультатЗапроса = Запрос.Выполнить()
    
    возврат РезультатЗапроса.Единственный().ТипЧата
    
;
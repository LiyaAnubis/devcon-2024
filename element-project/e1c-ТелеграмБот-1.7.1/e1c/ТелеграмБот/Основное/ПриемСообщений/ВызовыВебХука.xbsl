
@ВПодсистеме
метод ОбработкаВызова(Запрос: HttpСервисЗапрос)
    
    пер ТипКонтента = Запрос.Заголовки.ПолучитьПервый("Content-Type")

    если ТипКонтента.НачинаетсяС("application/json")
        ОбработкаВызоваJSON(Запрос)
    ;
    
;

метод ОбработкаВызоваJSON(Запрос: HttpСервисЗапрос)
    
    знч ВызовВебХука = новый ВызовВебХука(
        ТелоЗапроса = Запрос.Тело.ПрочитатьКакСтроку(),
        Запрос = Запрос)
        
    знч НастройкиОтладки = НастройкиПрограммы.ЗначениеНастройки(ВидНастройки.НастройкиОтладки)
    пер ЗаписьЛога: ИсторияВызововВебХука.Ссылка?

    если ОтладкаВключена(НастройкиОтладки)
        ЗаписьЛога = Телеграм.НачатьЗаписьЛога(ВызовВебХука.ТелоЗапроса)
    ;

    знч ДанныеЗапроса =
        СериализацияJson.ПрочитатьОбъект(ВызовВебХука.ТелоЗапроса) как Соответствие<Строка, неизвестно>

    если ДанныеЗапроса.СодержитКлюч("message")
        знч Сообщение = ДанныеЗапроса.Получить("message") как Соответствие<Строка, неизвестно>
        ОбработкаСообщения(Сообщение, ВызовВебХука)
    ;
    
    если ДанныеЗапроса.СодержитКлюч("callback_query")
        знч ОбратныйВызовТелеграм = ДанныеЗапроса.Получить("callback_query") как Соответствие<Строка, неизвестно>
        ОбработкаОбратногоВызова(ОбратныйВызовТелеграм, ВызовВебХука)
    ;
    
    если ДанныеЗапроса.СодержитКлюч("chat_member")
        знч ИзменениеУчастникаЧатаТелеграм = ДанныеЗапроса.Получить("chat_member") как Соответствие<Строка, неизвестно>
        ОбработкаИзмененияУчастникаЧата(ИзменениеУчастникаЧатаТелеграм, ВызовВебХука)
    ;

    ВызовВебХука.ПодготовитьОтветТелеграм()
    
    если ОтладкаВключена(НастройкиОтладки)
        Телеграм.ЗавершитьЗаписьЛога(ЗаписьЛога, Запрос.Ответ.КодСтатуса, ВызовВебХука.ТелоОтвета)
    ;
    
;

метод ОбработкаСообщения(Сообщение: Соответствие<Строка, неизвестно>, ВызовВебХука: ВызовВебХука)
    
    знч ВходящееСообщениеТелеграм = новый ВходящееСообщениеТелеграм()
    ВызовВебХука.ВходящееСообщение = ВходящееСообщениеТелеграм

    если Сообщение.СодержитКлюч("from")
        знч ОтправительТелеграм = Сообщение.Получить("from") как Соответствие<Строка, неизвестно>
        ВходящееСообщениеТелеграм.Автор = УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(ОтправительТелеграм)
    ;

    знч ЧатТелеграм = Сообщение.Получить("chat") как Соответствие<Строка, неизвестно>

    ВходящееСообщениеТелеграм.ИдентификаторСообщения = Сообщение.Получить("message_id") как Число
    ВходящееСообщениеТелеграм.Чат = Чаты.ИзОбъектаТелеграм(ЧатТелеграм)
    ВходящееСообщениеТелеграм.ТипЧата = Чаты.ТипЧата(ВходящееСообщениеТелеграм.Чат)
    
    если Сообщение.СодержитКлюч("text")
        ВходящееСообщениеТелеграм.ТекстСообщения = Сообщение.Получить("text") как Строка
    ;
    
    если Сообщение.СодержитКлюч("new_chat_members")
        ВходящееСообщениеТелеграм.НовыеУчастники = новый Массив<УчетныеЗаписиТелеграм.Ссылка>()
        знч НовыеУчастникиТелеграм = Сообщение.Получить("new_chat_members") как Массив<Объект?>
        для НовыйУчастникОбъект из НовыеУчастникиТелеграм
            знч НовыйУчастникТелеграм = НовыйУчастникОбъект как Соответствие<Строка, неизвестно>
            ВходящееСообщениеТелеграм.НовыеУчастники.Добавить(
                УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(НовыйУчастникТелеграм)
            )
        ;
    ;
    
    если Сообщение.СодержитКлюч("left_chat_member")
        знч ИсключенныйУчастникТелеграм = Сообщение.Получить("left_chat_member") как Соответствие<Строка, неизвестно>
        ВходящееСообщениеТелеграм.ИсключенныйУчастник = 
            УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(ИсключенныйУчастникТелеграм)
    ;

    знч Сервисы = ВходящиеСообщения.ПолучитьСервисы()

    для Сервис из Сервисы
        Сервис.ОбработатьВходящееТекстовоеСообщение(ВходящееСообщениеТелеграм)
    ;

;

метод ОбработкаОбратногоВызова(ОбратныйВызовТелеграм: Соответствие<Строка, неизвестно>, ВызовВебХука: ВызовВебХука)
    
    знч ЗапросОбратногоВызова = новый ЗапросОбратногоВызова()
    ВызовВебХука.ЗапросОбратногоВызова = ЗапросОбратногоВызова

    знч ИдентификаторТелеграм = ОбратныйВызовТелеграм.Получить("id") как Строка
    ЗапросОбратногоВызова.Идентификатор = ИдентификаторТелеграм
    
    знч ОтправительТелеграм = ОбратныйВызовТелеграм.Получить("from") как Соответствие<Строка, неизвестно>
    ЗапросОбратногоВызова.Автор = УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(ОтправительТелеграм)
    
    если ОбратныйВызовТелеграм.СодержитКлюч("data")
        знч ДанныеТелеграм = ОбратныйВызовТелеграм.Получить("data") как Строка
        ЗапросОбратногоВызова.Данные = ДанныеТелеграм
    ;
    
    если ОбратныйВызовТелеграм.СодержитКлюч("message")
        знч СообщениеТелеграм = ОбратныйВызовТелеграм.Получить("message") как Соответствие<Строка, неизвестно>
        ЗапросОбратногоВызова.ИдентификаторСообщения = СообщениеТелеграм.Получить("message_id") как Число
        знч ЧатТелеграм = СообщениеТелеграм.Получить("chat") как Соответствие<Строка, неизвестно>        
        ЗапросОбратногоВызова.Чат = Чаты.ИзОбъектаТелеграм(ЧатТелеграм)
        ЗапросОбратногоВызова.ТипЧата = Чаты.ТипЧата(ЗапросОбратногоВызова.Чат)
        если СообщениеТелеграм.СодержитКлюч("text")
            ЗапросОбратногоВызова.ТекстСообщения = СообщениеТелеграм.Получить("text") как Строка
        ;
    ;

    знч Сервисы = ВходящиеСообщения.ПолучитьСервисы()

    для Сервис из Сервисы
        Сервис.ОбработатьОбратныйВызов(ЗапросОбратногоВызова)
    ;
    
;


метод ОбработкаИзмененияУчастникаЧата(ИзменениеУчастникаЧатаТелеграм: Соответствие<Строка, неизвестно>, ВызовВебХука: ВызовВебХука)
    
    знч ИзменениеУчастникаЧата = новый ИзменениеУчастникаЧата()
    ВызовВебХука.ИзменениеУчастникаЧата = ИзменениеУчастникаЧата
    
    знч ЧатТелеграм = ИзменениеУчастникаЧатаТелеграм.Получить("chat") как Соответствие<Строка, неизвестно>
    ИзменениеУчастникаЧата.Чат = Чаты.ИзОбъектаТелеграм(ЧатТелеграм)
    ИзменениеУчастникаЧата.ТипЧата = Чаты.ТипЧата(ИзменениеУчастникаЧата.Чат)
    
    знч ОтправительТелеграм = ИзменениеУчастникаЧатаТелеграм.Получить("from") как Соответствие<Строка, неизвестно>
    ИзменениеУчастникаЧата.Автор = УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(ОтправительТелеграм)
    
    знч СтароеОписаниеТелеграм = ИзменениеУчастникаЧатаТелеграм.Получить("old_chat_member") как Соответствие<Строка, неизвестно>
    знч СтарыйУчастникТелеграм = СтароеОписаниеТелеграм.Получить("user") как Соответствие<Строка, неизвестно>
    знч СтатусСтарогоУчастникаТелеграм = СтароеОписаниеТелеграм.Получить("status") как Строка
    ИзменениеУчастникаЧата.СтарыйУчастник = УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(СтарыйУчастникТелеграм)
    ИзменениеУчастникаЧата.СтарыйСтатус = СтатусУчастникаЧата(СтатусСтарогоУчастникаТелеграм)
     
    знч НовоеОписаниеТелеграм = ИзменениеУчастникаЧатаТелеграм.Получить("new_chat_member") как Соответствие<Строка, неизвестно>
    знч НовыйУчастникТелеграм = НовоеОписаниеТелеграм.Получить("user") как Соответствие<Строка, неизвестно>
    знч СтатусНовогоУчастникаТелеграм = НовоеОписаниеТелеграм.Получить("status") как Строка
    ИзменениеУчастникаЧата.НовыйУчастник = УчетныеЗаписиТелеграм.ИзОбъектаТелеграм(НовыйУчастникТелеграм)
    ИзменениеУчастникаЧата.НовыйСтатус = СтатусУчастникаЧата(СтатусНовогоУчастникаТелеграм)
   
    знч Сервисы = ВходящиеСообщения.ПолучитьСервисы()

    для Сервис из Сервисы
        Сервис.ОбработатьИзменениеУчастникаЧата(ИзменениеУчастникаЧата)
    ;
    
;

@Локально
метод СтатусУчастникаЧата(СтатусНовогоУчастникаТелеграм: Строка): СтатусУчастникаЧата
    
    выбор СтатусНовогоУчастникаТелеграм
    когда == "creator"
        возврат СтатусУчастникаЧата.Владелец
    когда == "administrator"
        возврат СтатусУчастникаЧата.Администратор
    когда == "member"
        возврат СтатусУчастникаЧата.Участник
    когда == "restricted"
        возврат СтатусУчастникаЧата.Ограниченный
    когда == "left"
        возврат СтатусУчастникаЧата.Покинувший
    когда == "kicked"
        возврат СтатусУчастникаЧата.Исключенный
    иначе
        возврат СтатусУчастникаЧата.Участник
    ;
    
;

метод ОтладкаВключена(НастройкиОтладки: Строка): Булево
    если НастройкиОтладки == "РежимОтладки"
        возврат Истина
    ;
    возврат Ложь
;

@ВПодсистеме
метод ПодготовитьОтветТелеграм()    
    
    если ВходящееСообщение != Неопределено
        ПодготовитьОтветНаВходящееСообщение()
        возврат
    ;
    
    если ЗапросОбратногоВызова != Неопределено
        ПодготовитьОтветНаЗапросОбратногоВызова()
        возврат
    ;
    
    если ИзменениеУчастникаЧата != Неопределено
        ПодготовитьОтветНаИзменениеУчастникаЧата()
        возврат
    ;
    
;

@Локально
метод ПодготовитьОтветНаЗапросОбратногоВызова()
    
    знч ОтветноеДействие = ЗапросОбратногоВызова?.ОтветноеДействие

    ПодготовитьОтвет(ОтветноеДействие)
            
;

@Локально
метод ПодготовитьОтветНаВходящееСообщение()
    
    знч ОтветноеДействие = ВходящееСообщение?.ОтветноеДействие

    ПодготовитьОтвет(ОтветноеДействие)
        
;

@Локально
метод ПодготовитьОтветНаИзменениеУчастникаЧата()
    
    знч ОтветноеДействие = ИзменениеУчастникаЧата?.ОтветноеДействие

    ПодготовитьОтвет(ОтветноеДействие)
        
;

@Локально
метод ПодготовитьОтвет(ОтветноеДействие: ИсходящееСообщение|РеакцияНаСообщение|УдалениеСообщения|?)
    
    если ОтветноеДействие == Неопределено
        возврат
    ;

    если ОтветноеДействие это УдалениеСообщения
        знч ДанныеОтвета = (ОтветноеДействие как УдалениеСообщения).ВОбъектТелеграм()
        ДанныеОтвета.Вставить("method", МетодыТелеграм.УдалитьСообщение())
        УстановитьОтветJSON(ДанныеОтвета)
    ;

    если ОтветноеДействие это ИсходящееСообщение
        знч ДанныеОтвета = (ОтветноеДействие как ИсходящееСообщение).ВОбъектТелеграм()
        ДанныеОтвета.Вставить("method", МетодыТелеграм.ОтправитьСообщение())
        УстановитьОтветJSON(ДанныеОтвета)
    ;
    
    если ОтветноеДействие это РеакцияНаСообщение
        знч ДанныеОтвета = (ОтветноеДействие как РеакцияНаСообщение).ВОбъектТелеграм()
        ДанныеОтвета.Вставить("method", МетодыТелеграм.УстановитьРеакциюНаСообщение())
        УстановитьОтветJSON(ДанныеОтвета)
    ;
    
;


@Локально
метод УстановитьОтветJSON(ДанныеОтвета: Соответствие<Строка, Объект?>)
    ТелоОтвета = СериализацияJson.ЗаписатьОбъект(ДанныеОтвета)

    Запрос.Ответ.Заголовки.Добавить("Content-Type", "application/json")
    Запрос.Ответ.УстановитьТело(ТелоОтвета)
;